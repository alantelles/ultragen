include @Core.Web.Cookie
include @Core.DateTime
include @Core.FileSystem

class Session

Session.key = 'UNSET_KEY'
Session.duration = null

function start() : Session
    appResponseIsSet = assigned AppResponse    
    if (!appResponseIsSet)
        raise("No AppResponse handler is set for this application", 'Session')
    end
    include @Core.OS    
    include @Core.FileSystem
    include @Core.Helpers
    Session.path = (OS.getEnv('ULTRAGEN_HOME')) + '/' + 'sessions/' + (Session.key)
    FileSystem.mkdir(Session.path, true)
    Session.erase = false    
    full_key = 'UGAPPSESSID_' + (Session.key)
    started = $request[:cookies].get(full_key)
    Session.content = {"__DATA__": {}}
    Session.flashMessages = []
    if(!started)
        Session.id = Helpers.randomToken(64, true, true, false)
        if (!(Session.duration))
            AppResponse.$cookies['UGAPPSESSID_' + (Session.key)] = Session.id
        else
            if (typeof(Session.duration) != ('Integer'))
                raise ("Invalid type for session duration", "Session")
            end
            AppResponse.$cookies['UGAPPSESSID_' + (Session.key)] = new Cookie(Session.id, {'max-age': Session.duration})
        end
    else
        Session.id = started
        session_path = (Session.path) + '/' + (Session.id)
        if ((FileSystem.isFile(session_path)))
            Session.content = JSON.parseFile(session_path)
        end
    end
end

function flash(message, category="primary") : Session
    Session.flashMessages.append({"message": str(message), "category": str(category)})
end

function getFlashMessages() : Session
    hasFlash = Session.content.hasKey('__FLASH__')
    msgs = []
    if (hasFlash)        
        msgs = Session.content["__FLASH__"]
    end
    Session.content["__FLASH__"] = {}
    return msgs
end

function get(key, ifNot=null) : Session
    x = Session.content["__DATA__"].get(key)
    return x
end

function set(key, value) : Session
    Session.content["__DATA__"][key] = value
end

function unset(key) : Session
    if((Session.get(key)))
        Session.content["__DATA__"].drop(key)
    end
end

function destroy() : Session
    full_key = 'UGAPPSESSID_' + (Session.key)
    id = $request['cookies'].get(full_key)
    AppResponse.$cookies[full_key] = Cookie.expire()
    Session.erase = true
    if (id)
        session_path = (Session.path) + '/' + str(id)
        exist = FileSystem.isFile(session_path)
        if(exist)
            FileSystem.delete(session_path)
        end
    end
end

function save() : Session
    Session.content['__FLASH__'] = Session.flashMessages    
    # hasData = (Session.content["__DATA__"].keys().length()) != 0
    # hasFlash = (Session.content["__FLASH__"].length()) != 0
    if(!(Session.erase))
        session_path = (Session.path) + '/' + (Session.id)
        JSON.createFile(Session.content, session_path)
    end
end