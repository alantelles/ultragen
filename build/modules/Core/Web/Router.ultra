class Router
function init() : Router
    self.routes = []
    routeModel = """{
        "path": '/some',
        "handler": SomeFunction,
        "name": RouteIdentifier(optional),
        "method": [some http verb]
    }"""
end

function HandlerFor404(params) : Router
    live 'Page not found'
end

function urlFor(name, params) : Router
    for (self.routes, match)
        if (name == (match[:name]))
            matched = Router.dropFirstAndLastSlash(match[:path])
            matchedSplit = matched.split('/')
            len = matchedSplit.length()
            output = ['']
            queryStr = ''
            for (matchedSplit, part)
                partIsParam = (part[0]) == ':'
                if (partIsParam)
                    paramName = part.slice(1, part.length())
                    ins = params[paramName]
                    params.drop(paramName)
                    output.append(str(ins))
                else
                    output.append(part)
                end
            end
            strOut = '/'.join(output)
            extraParams = params.keys()
            if ((extraParams.length()) > 0)
                queryStr = '?'
                queryList = []
                for (extraParams, extra)
                    queryList.append(extra + '=' + str(params[extra]))
                end
                queryStr += ('&'.join(queryList))
            end
            if (strOut == '')
                strOut = '/'
            end
            return strOut + queryStr
        end
    end
    return null
end

function dropFirstAndLastSlash(route) : Router
    if ((route.length()) > 0)
        f = route[0]
    else
        f = ''
    end
    
    len = route.length()
    if (f == '/')
        route = route.slice(1, len)
    end
    l = route.last()
    len = route.length()
    if ((l == '/') && (len > 0))
        route = route.slice(0, len - 1)
    end
    return route
end

function getMatch(route, method) : Router 
    route = Router.dropFirstAndLastSlash(route)
    routeSplit = route.split('/')
    lenRoute = routeSplit.length()
    for (self.routes, match)        
        routeMatch = true
        if(method == (match[:method]))
            cand = Router.dropFirstAndLastSlash(match[:path])
            candSplit = cand.split('/')
            lenCand = candSplit.length()
            
            if (lenCand == lenRoute)
                params = {}
                for (candSplit, part)
                    partIsParam = (part[0]) == ':'
                    if (part == (routeSplit[_part]))
                    
                    elsif (partIsParam)
                        paramName = part.slice(1, part.length())
                        params[paramName] = routeSplit[_part]
                    else
                        routeMatch = false
                        break
                    end
                end
            else
                routeMatch = false
                continue
            end
            if (routeMatch)
                return {:handler: match[:handler], :params: params}
            end
            
        end
    end
    return {:handler: self.HandlerFor404, :params: {}}
end

function addRoute(path, name, handler, method) : Router
    self.routes.append({
        :path: path,
        :name: name,
        :handler: handler,
        :method: method
    })
end

function get(path, name, handler) : Router
    self.addRoute(path, name, handler, 'GET')
end

function post(path, name, handler) : Router
    self.addRoute(path, name, handler, 'POST')
end

function delete(path, name, handler) : Router
    self.addRoute(path, name, handler, 'DELETE')
end

function patch(path, name, handler) : Router
    self.addRoute(path, name, handler, 'PATCH')
end

function put(path, name, handler) : Router
    self.addRoute(path, name, handler, 'PUT')
end