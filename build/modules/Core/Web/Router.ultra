include @Core.DateTime

class Router
function init(name_prefix='', url_prefix='') : Router
    self.name_prefix = name_prefix
    self.url_prefix = url_prefix
    self.routes = []
    routeModel = """{
        "path": '/some_path',
        "handler": SomeFunctionReference,
        "name": :identifier,
        "method": [some http verb]
    }"""
end

function HandlerFor404(params) : Router
    live 'Page not found'
end

function urlFor(name, params={}) : Router
    for (self.routes, match)
        if (name == (match[:name]))
            matched = dropFirstAndLastSlash(match[:path])
            
            matchedSplit = matched.split('/')
            len = matchedSplit.length()
            output = ['']
            queryStr = ''
            
            for (matchedSplit, part)
                partIsParam = (part[0]) == ':'
                
                if (partIsParam)
                    paramName = part.slice(1, part.length())
                    ins = params[paramName]
                    params.drop(paramName)
                    output.append(str(ins))
                else
                    output.append(part)
                end
            end
            strOut = '/'.join(output)
            extraParams = params.keys()
            if ((extraParams.length()) > 0)
                queryStr = '?'
                queryList = []
                for (extraParams, extra)
                    queryList.append(extra + '=' + str(params[extra]))
                end
                queryStr += ('&'.join(queryList))
            end
            if (strOut == '')
                strOut = '/'
            end
            k = dropFirstAndLastSlash(self.url_prefix)
            return k + strOut + queryStr
        end
    end
    raise("The given name '" + name + "' doesn't match any url in this router", 'Router')
end

function dropFirstAndLastSlash(aRoute)
    route = aRoute
    if ((route.length()) > 0)
        f = route[0]
    else
        f = ''
    end
    
    len = route.length()
    if (f == '/')
        route = route.slice(1, len)
    end
    l = route.last()
    len = route.length()
    if ((l == '/') && (len > 0))
        route = route.slice(0, len - 1)
    end
    
    return route
end

function getMatch(route=null, method=null) : Router
    if (!method)
        method = $request[:method]
    end
    if (!route)
        route = $request[:route]
    end
    route = dropFirstAndLastSlash(route)
    
    routeSplit = route.split('/')
    lenRoute = routeSplit.length()
    for (self.routes, match)        
        routeMatch = true
        if(method == (match[:method]))
            
            cand = dropFirstAndLastSlash(match[:path])
            
            candSplit = cand.split('/')
            lenCand = candSplit.length()
            
            if (lenCand == lenRoute)
                params = {}
                for (candSplit, part)
                    partIsParam = (part[0]) == ':'
                    
                    if (part == (routeSplit[_part]))
                        
                    elsif (partIsParam)

                        
                        paramName = part.slice(1, part.length())
                        params[paramName] = routeSplit[_part]
                    else
                        
                        routeMatch = false
                        break
                    end
                end
            else
                routeMatch = false
                continue
            end
            if (routeMatch)
                
                return {:handler: match[:handler], :params: params}
            end
            
        end
    end
    return {:handler: self.HandlerFor404, :params: {}}
end

function addRouter(router) : Router
    
    
    for(router.routes, route)
        newRoute = {
            :path: '/' + (dropFirstAndLastSlash(router.url_prefix)) + '/' + (dropFirstAndLastSlash(route[:path])),
            :name: (router.name_prefix) + '_' + (route[:name]),
            :handler: route[:handler],
            :method: route[:method]
        }
        self.routes.append(newRoute)
    end
    
end

function addRoute(path, name, handler, method) : Router
    self.routes.append({
        :path: path,
        :name: name,
        :handler: handler,
        :method: method
    })
end

function get(path, name, handler) : Router
    self.addRoute(path, name, handler, 'GET')
end

function post(path, name, handler) : Router
    self.addRoute(path, name, handler, 'POST')
end

function delete(path, name, handler) : Router
    self.addRoute(path, name, handler, 'DELETE')
end

function patch(path, name, handler) : Router
    self.addRoute(path, name, handler, 'PATCH')
end

function put(path, name, handler) : Router
    self.addRoute(path, name, handler, 'PUT')
end